plugins {
    // project plugins
    id 'java-gradle-plugin'
    id 'groovy'

    id 'signing'

    // test coverage
    id 'jacoco'

    // ide plugin
    id 'idea'

    // intershop test plugin
    id 'com.intershop.gradle.test' version '2.2.0'

    // plugin for documentation
    id 'org.asciidoctor.convert' version '1.5.3'

    // publish plugin
    id 'maven-publish'

    // plugin for publishing to Gradle Portal
    id 'com.gradle.plugin-publish' version '0.20.0'
}

// release configuration
group 'com.intershop.gradle.scm'
description 'Gradle SCM version plugin - SCM based version handling for Gradle'
version = '2.7.4'


sourceCompatibility = 1.8
targetCompatibility = 1.8

// set correct project status
if (project.version.endsWith('-SNAPSHOT')) {
    status = 'snapshot'
}

println(".. 1 .. GitUrl: " + System.getenv('GITURL'))

// test configuration
test {
    if(System.getenv('SVNUSER') && System.getenv('SVNPASSWD') && System.getenv('SVNURL')) {
        systemProperty 'svnurl', System.getenv('SVNURL')
        systemProperty 'svnuser', System.getenv('SVNUSER')
        systemProperty 'svnpasswd', System.getenv('SVNPASSWD')
    }
    if(System.getenv('GITUSER') && System.getenv('GITPASSWD') && System.getenv('GITURL')) {
        systemProperty 'giturl', System.getenv('GITURL')
        systemProperty 'gituser', System.getenv('GITUSER')
        systemProperty 'gitpasswd', System.getenv('GITPASSWD')

        println(".... GitUrl: " + System.getenv('GITURL'))
    }

    if(System.getProperty('SVNUSER') && System.getProperty('SVNPASSWD') && System.getProperty('SVNURL')) {
        systemProperty 'svnurl', System.getProperty('SVNURL')
        systemProperty 'svnuser', System.getProperty('SVNUSER')
        systemProperty 'svnpasswd', System.getProperty('SVNPASSWD')
    }

    if(System.getProperty('GITUSER') && System.getProperty('GITPASSWD') && System.getProperty('GITURL')) {
        systemProperty 'giturl', System.getProperty('GITURL')
        systemProperty 'gituser', System.getProperty('GITUSER')
        systemProperty 'gitpasswd', System.getProperty('GITPASSWD')
    }

    //Change directory for gradle tests
    systemProperty 'org.gradle.native.dir', '.gradle'
    //Set supported Gradle version
    systemProperty 'intershop.gradle.versions', '2.11'
}

task copyAsciiDoc {
    def outputDir = file("$buildDir/tmp/asciidoctorSrc")
    def inputFiles = fileTree(dir: rootDir, include: '**/*.asciidoc')

    inputs.files inputFiles
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()

        copy {
            from inputFiles
            into outputDir
        }
    }
}

asciidoctorj {
    noDefaultRepositories = true
}

asciidoctor {
    sourceDir "${buildDir}/tmp/asciidoctorSrc"
    sources {
        include 'README.asciidoc'
    }

    backends 'html5', 'docbook'
    options doctype : 'article',
            ruby    : 'erubis'
    attributes  'latestRevision'     : project.version,
            'toc':'left',
            'toclevels':'2',
            'source-highlighter' : 'coderay',
            'icons': 'font',
            'setanchors':'true',
            'idprefix':'asciidoc_',
            'idseparator':'-',
            'docinfo1':'true'
}

asciidoctor.dependsOn copyAsciiDoc

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task groovydocJar(type: Jar) {
    dependsOn groovydoc
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

// additional sources artifact
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourceJar
    archives groovydocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        intershopMvn(MavenPublication) {
            from components.java

            pom.withXml {
                def root = asNode()

                // eliminate test-scoped dependencies (no need in maven central POMs)
                root.dependencies.removeAll { dep ->
                    dep.scope == "test"
                }

                // add all items necessary for maven central publication
                root.children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    description "${project.description}"
                    name "${project.name}"
                    url "https:/gitlab.intershop.de/IntershopGradlePlugins/${project.name}"
                    organization {
                        name 'Intershop Communications'
                        url 'http://intershop.com'
                    }
                    issueManagement {
                        system 'GitHub'
                        url "https://gitlab.intershop.de/IntershopGradlePlugins/${project.name}/issues"
                    }
                    licenses {
                        license {
                            name 'Apache License 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }
                    scm {
                        url "https://github.com/IntershopCommunicationsAG/${project.name}"
                        connection "scm:git:https://gitlab.intershop.de/IntershopGradlePlugins/${project.name}.git"
                        developerConnection "git@github.com:/IntershopCommunicationsAG/${project.name}.git"
                    }
                    developers {
                        developer {
                            id 'm-raab'
                            name 'M. Raab'
                            email 'mraab@intershop.de'
                        }
                    }
                }

                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact sourceJar {
                classifier 'sources'
            }

            artifact groovydocJar {
                classifier "javadoc"
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

model {
    tasks.generatePomFileForIntershopMvnPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishIntershopMvnPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishIntershopMvnPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

publish.dependsOn asciidoctor

pluginBundle {
    website = "https://github.com/IntershopCommunicationsAG/${project.name}"
    vcsUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"
    description = project.description
    tags = ['intershop', 'gradle', 'plugin', 'test']

    plugins {
        gradleTestPlugin {
            id = 'com.intershop.gradle.scmversion'
            displayName = project.description
        }
    }
}

dependencies {
    compile 'com.intershop.gradle.version:extended-version:3.1.0'

    //svn
    // replace svnkit's JNA 4.x with 3.2.7, which is used by Gradle itself
    compile('org.tmatesoft.svnkit:svnkit:1.9.3') {
        exclude group: 'net.java.dev.jna'
        exclude group: 'com.trilead', module: 'trilead-ssh2'
    }
    compile 'com.trilead:trilead-ssh2:1.0.0-build221'
    runtime 'net.java.dev.jna:jna:3.2.7'

    //jgit
    compile('org.eclipse.jgit:org.eclipse.jgit:5.1.3.201810200350-r') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    runtime 'org.apache.httpcomponents:httpclient:4.4.1'
    runtime 'org.slf4j:slf4j-api:1.7.10'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'commons-io:commons-io:2.2'
}

repositories {
    mavenCentral()
}
